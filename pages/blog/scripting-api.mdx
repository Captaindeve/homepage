export const meta = {
  title: 'open.mp API design',
  date: '2020-04-14T20:01:00',
  author: 'Y_Less'
};

# open.mp scripting

## Introduction

Firstly, a VERY important clarification - existing scripts will still work as-is.  We have worked
very hard on backwards compatibility and bear it in mind for every decision.  There are many
improvements we'd like to make that we simply can't for this reason, and other code that is greatly
complicated by this compatibility requirement.

That said, there are improvements that can be made all over the place.  Let's look at some examples
of the inconsistencies in SA:MP scripting:

### Tags

* `Menu:CreateMenu` - Tagged.
* `DB:db_open` - Tagged.
* `CreateVehicle` - Untagged.
* `CreateActor` - Untagged.

```pawn
#define SELECT_OBJECT_GLOBAL_OBJECT 1
#define SELECT_OBJECT_PLAYER_OBJECT 2

forward OnPlayerSelectObject(playerid, type, objectid, modelid, Float:fX, Float:fY, Float:fZ);
```

`type` is untagged, as are ALL SA:MP defined constants; unlike pawn default constants:

```pawn
native File:fopen(const name[], filemode:mode = io_readwrite);
```

### Naming

* `SetVehiclePos` - "Vehicle" in the middle of the function name.
* `TextDrawTextSize` - "TextDraw" at the start.
* `db_open` - "db" at the start and lower-case.
* `fread` - "file" at the start, and shortened.
* `asin` - A SA:MP added function without camel/pascal case.

Consistency:

* `GetVehicleZAngle` - "Z-Angle"
* `GetVehicleRotationQuat` - "Rotation"
* `SetPlayerFacingAngle` - "Facing Angle"
* `SetObjectRot` - "Rot"

And despite all this, most libraries have now settled on the `Module_Method` naming convention.

### Constants

* `65535`

Value for invalid players, actors, TDs and a few other things.  It is also the value for invalid
vehicles, but `0` is ALSO an invalid vehicle ID sometimes returned.

* `0`

Value for invalid files, sometimes vehicles (as well as `65535`).  Also the value for missing many
things such as action states and weapons.

* `255`

Value for invalid teams and menus.

* `-1`

Value for invalid gang zones and weapon states.

Additionally many libraries use `0x80000000` and `-1` for invalid states because they are far less
likely to eventually be a valid ID.  65535 is quite a large number, but a big server can easily
have more objects than that.

### Per-Player Functions

* `CreateObject` and `CreatePlayerObject`

Has global and per-player versions.

* `SetPlayerMapIcon`

Has no global version.

* `SetGravity`

No per-player version, despite being possibly one of the most requested per-player functions, and
added almost instantly by YSF and other plugins.

* `CreateVehicle`

No per-player version, despite also being repeatedly requested.  But also not added by any (public)
plugin, not even the streamer plugin.

* `SendClientMessage` and `SendClientMessageToAll`

Has global and per-player versions, but the per-player version is the default unlike most other
functions.

* `GangZoneShowForPlayer` and `GangZoneShowForAll`

Menus, Gang Zones, and Text Draws are the only default SA:MP functions where you can specify exactly
which players can see them.  All others are either everyone or just one person.  Of course,
libraries and plugins have since vasly expanded on this model and most good ones now allow very
fine-grained control over which subsets of players (groups) can use any given entity.

### IDs

* `CreateObject` and `CreatePlayerObject`

The IDs pool for these functions are shared.  If a global object has ID `4` no player object can
ever have ID `4`, but multiple players could have different objects all with ID `5`.

* `Create3DTextLabel` and `CreatePlayer3DTextLabel`

The ID pool is split - the first `1024` IDs are globals, the second `1024` are per-player.  Each
player can have up to `2048` 3D texts, but only `1024` of each type despite the fact that it makes
no difference client-side.

* `SetPlayerMapIcon`

IDs are manually specified, up to a limit of 32.  For a while this limit was not checked client-
side, leading to a potential ACE exploit.

* `ShowPlayerDialog`

IDs are manually specified, with no limit at all.  The IDs are also totally pointless since a player
can only ever see one dialog at once.

* `SetTimer`

IDs wrap around, with no checking that an existing timer has the same ID.  You would have to start
over 4,000,000,000 timers at some point to encounter this issue, but it could happen - they don't
even have to all remain running.

And of course some people rely on IDs being allocated in very specific orders, then wonder why their
whole mode breaks when they for example add or remove a single vehicle.

## Compatibility

So, again, we must make it very clear that all existing *"SA:MP code"* will work.  What does that
mean exactly?  Any code that is:

1. Written in pawn.
2. Uses the original SA:MP API without plugins.
3. Is recompiled with our includes.
4. Already uses the community compiler.

Will work.

However:

* If you use a plugin to write in a language other than pawn, that plugin will probably need to be
ported first.  So your code won't work immediately.

* If you use other plugins such as the streamer, YSF, audio, etc; they may already work, may need
porting, or may be entirely superfluous because their functionality has been integrated in to the
core server.  So your code may work.

* If you only have the .AMX of your mode not the original source, why?  Anyway, while all SA:MP
functions exist, some have been redone or replaced by pawn code or macros and you MUST recompile.
So if you can't, your code won't work at all.

## Building

### Eample

Let's look at an insanely simple SA:MP mode to begin with.

```pawn
#include <a_samp>

main() {}

public OnGameModeInit()
{
	SetGameModeText("Example Script");
	AddPlayerClass(0, 0.0, 0.0, 4.0, 0.0, 0, 0, 0, 0, 0, 0);
	return 1;
}

public OnPlayerSpawn(playerid)
{
	SetPlayerCheckpoint(playerid, 20.0, 20.0, 4.0, 2.0);
	return 1;
}

public OnPlayerEnterCheckpoint(playerid)
{
	SendClientMessage(playerid, 0xFF0000AA, "You won!");
	return 1;
}
```

You spawn.  You go to the checkpoint.  You win.

### Conversion

To build this for open.mp, we need to change just the first include and add one define.

This:

```pawn
#include <a_samp>

main() {}
```

Becomes:

```pawn
#define OPENMP_COMPAT
#include <openmp\openmp>

main() {}
```

The first error you might get is:

`open.mp scripts require the community compiler from: git.io/pawn-compiler`

If you get this, go to https://git.io/pawn-compiler and download compiler version 3.10.10 or later.
For the release we would like a pawno-equivalent tool with this compiler integrated already, but
haven't done that yet.  I STRONGLY suggest trying to compile your mode with this compiler first as
it re-enabled const correctness warnings so you're likely to have a load of new warnings straight
away (this is NOT a problem with the compiler, these are problems in your code that always existed,
but were previously ignored).  You will also probably want to replace your includes with these ones:

https://github.com/pawn-lang/pawn-stdlib

https://github.com/pawn-lang/samp-stdlib

That's a good thing to do even if you don't use open.mp, as they fix a load of tag and const issues
in the original includes.

### Warnings

If you don't get any warnings using the new compiler with the new version of *a_samp*, you will now
see a load of new warnings along the lines of:

`warning 234: function is deprecated (symbol "AddPlayerClass") Use "Class_Add" instead.`

You have three options - and they're all supported:

1. **Ignore the warnings:**  The code will still work.

2. **Suppress the warnings:**  Change `OPENMP_COMPAT` to `OPENMP_QUIET`:

```pawn
#define OPENMP_QUIET
#include <openmp\openmp>

main() {}
```
